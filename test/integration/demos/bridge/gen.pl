#!/usr/bin/perl -w

################################################################################
#
# Copyright (c) 2000-2015 Liferay, Inc. All rights reserved.
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#     http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
################################################################################

################################################################################
#
# This Perl script is a test generator for helping keep all changes made to 
# the job applicant selenium tests in one .java file called.
# from that template all other job applicant Test.java files are generated by
# this perl script.
#
################################################################################
#
# Author: Vernon Singleton
#
################################################################################


use strict;

# primitives
my $issueFile = "faces-issues";
my $template = "./job-application-portlet/src/test/java/com/liferay/faces/test/JobPortletTest.java";
my $dateValidationXpathModifier;
my $variable;
my $javaFile;
my($issue, $affected, $test, $method, $assertion);
my $annotation;
my $testClass;
my $xpath;
my $lineNumber = 0;
my $line;
my $dir;
my $url;
my $foo;

# hashes
my %in = (
   JsfApplicantPortletTest => "./jsf-applicant-portlet/src/test/java/com/liferay/faces/test/JsfApplicant.java",
   JsfCdiApplicantPortletTest => "./jsf-cdi-applicant-portlet/src/test/java/com/liferay/faces/test/JsfCdiApplicant.java",
   JsfHtml5ApplicantPortletTest => "./jsf-html5-applicant-portlet/src/test/java/com/liferay/faces/test/JsfHtml5Applicant.java",
   JsfSpringApplicantPortletTest => "./jsf-spring-applicant-portlet/src/test/java/com/liferay/faces/test/JsfSpringApplicant.java",
   AlloyApplicantPortletTest => "./alloy-applicant-portlet/src/test/java/com/liferay/faces/test/AlloyApplicant.java",
   IcefacesApplicantPortletTest => "./icefaces-applicant-portlet/src/test/java/com/liferay/faces/test/IcefacesApplicant.java",
   RichfacesApplicantPortletTest => "./richfaces-applicant-portlet/src/test/java/com/liferay/faces/test/RichfacesApplicant.java",
   PrimefacesApplicantPortletTest => "./primefaces-applicant-portlet/src/test/java/com/liferay/faces/test/PrimefacesApplicant.java",
);
my %variables = ();
my %xpaths = ();
my %annotations = ();
my %methods = ();
my %out = ();

# lists
my @class = keys %in;
my @path;
my @bar;

# parse the issues from the issues file
if ( -f "$issueFile" ) {
   open(ISSUES, $issueFile) or die "cannot open $issueFile: $!\n";
   while (<ISSUES>) {
      chomp;
      next unless /^FACES/;
      ($issue, $affected, @bar) = split;
      $_ = $affected;
      /(\w+)\(/; $method = $1;
      /\.(\w+)\):/; $testClass= $1;
      $methods{$testClass . $method} = $issue;
   }
   close ISSUES;
} else {
   print "INFO: no issuesFile ... \n";
}

print "INFO: using template: $template\n\n";

# parse the Xpath variable names in from the template .java file
open(TEMPLATE, $template) or die "cannot open $template: $!\n";
while (<TEMPLATE>) {
   $lineNumber += 1;
   chomp;
   if (/private..*Xpath/) {
      ($foo,$foo,$foo,$foo,$variable,$foo,@path) = split;
      $xpath = join " ", @path;
      $variables{$variable} = 1;
      # print "$variable\n";
   }
   if (/protected..*Xpath/) {
      ($foo,$foo,$foo,$foo,$variable,$foo,@path) = split;
      $xpath = join " ", @path;
      $variables{$variable} = 1;
      # print "$variable\n";
   }
   if (/\@Test/) {
      $annotation = $lineNumber;
   }
   if (/public void \w+\(\) throws Exception/) {
      # public void jobApplicantRenderViewMode() throws Exception
      ($foo,$foo,$method,@bar) = split;
      $_ = $method;
      s/\(\)//;
      $method = $_;
      # print "method = $method\n";
      if (defined $annotation) {
         $annotations{$annotation} = $method;
      }
   }
}
close TEMPLATE;

# iterate through the Test.java classes
foreach my $class (@class) {

   # get the output file name
   print "INFO: class = $class\n";
   $javaFile = "${class}.java";

   # get the output directory name
   $_ = $in{$class};
   s/\w+\.java//;
   $dir = $_;

   # get the full path to the output file
   $out{$class} = $dir . $javaFile;
   # print "$dir $javaFile\n";

   # parse the Xpaths in that are specific to the component library being tested
   print "INFO: getting xpaths from $in{$class}\n";
   open(IN, $in{$class}) or die "cannot open $in{$class}: $!\n";
   while (<IN>) {
      chomp;
      if(/static final String url =/) {
         $url = $_;
      }
      if (/private..*Xpath/) {
         ($foo,$foo,$foo,$foo,$variable,$foo,@path) = split;
         $xpath = join " ", @path;
         $xpaths{$variable} = $xpath;
         # print "private $variable $xpath\n";
      }
      if (/protected..*Xpath/) {
         ($foo,$foo,$foo,$foo,$variable,$foo,@path) = split;
         $xpath = join " ", @path;
         $xpaths{$variable} = $xpath;
         # print "protected $variable $xpath\n";
      }
      if (/protected int dateValidationXpathModifier/) {
         $dateValidationXpathModifier = $_;
         # print "dateValidationXpathModifier=$dateValidationXpathModifier\n";
      }
   }
   close IN;

   # write the output file, using the TEMPLATE file
   open(TEMPLATE, $template) or die "cannot open $template: $!\n";
   open(OUT, ">$out{$class}") or die "cannot open $out{$class} for writing: $!\n";
   $lineNumber = 0;
   while (<TEMPLATE>) {
      ++$lineNumber;
      chomp;
      if (/public class/) {
         # print "$_\n";
         print OUT "public class $class extends TesterBase \{\n";
      } elsif (/protected final static Logger/) {
         print "	protected final static Logger logger = Logger.getLogger(${class}.class.getName());\n";
         print OUT "	protected final static Logger logger = Logger.getLogger(${class}.class.getName());\n";
      } elsif (/static final String url =/) {
         print "INFO: $url\n";
         print OUT "$url\n";
      } elsif (/private..*Xpath/) {
         ($foo,$foo,$foo,$foo,$variable,$foo,@path) = split;
         if (not defined $variable) {
            print "WARNING: variable undefined after finding /private..*Xpath/ in $template: line = $_\n";
         } else {
            if (not defined $xpaths{$variable}) {
               print "INFO: no xpath found for variable = $variable: line = $_\n";
               $xpath = join " ", @path;
               $xpaths{$variable} = $xpath;
               print "INFO: inserting the one found in the template file ... check it :-)\n";
            }
         }
         print OUT "	private static final String $variable = $xpaths{$variable}\n";
      } elsif (/protected..*Xpath/ and not /protected..*XpathMod/) {
         ($foo,$foo,$foo,$foo,$variable,$foo,@path) = split;
         print OUT "	protected static final String $variable = $xpaths{$variable}\n";
      } elsif (/protected int dateValidationXpathModifier/) {
         print OUT "$dateValidationXpathModifier\n";
      } elsif (defined $annotations{$lineNumber}) {
         if (defined $methods{$class . $annotations{$lineNumber}}) {
            $issue = $methods{$class . $annotations{$lineNumber}};
            $method = $annotations{$lineNumber};
            print OUT "// $issue covers an issue tested by ${method}.  Please refer to $issue for clarification.\n";
            print OUT "// $_\n";
         } else {
            print OUT "$_\n";
         }
      } else {
         print OUT "$_\n";
      }
   }
   close OUT;
   close TEMPLATE;
   print "\n";
}

